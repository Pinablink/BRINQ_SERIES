;----------------------------------------------------------------------------------------------------------------------
;       #######################***#################%%%%%%%##
;       #############********###############################
;       ###########*******#########****#%%%%################
;       ####*##*********####******###****#%%%########***####
;       #*************##************###***+*#%##############
;       #***********###***************##***++*%%%%##########
;       #####******###**********++***#####**++*%%%%%%#######
;       #####****####*********+++++**#####**+==*%%%%%%######
;       ####*****%###*********+++++***####**+=--%@@%%%######
;       ####****%%%#*#******++++++*+**###**+==-:+@@%%%%####%
;       %%###**#%%#####******++===++***#**++==---%@%%%##%%%%
;       %%######%%#######*****++===+***#*+===-::-*%%%%####%%
;       %######%%%#######*****+++==+***#*+--==:::=%%%%%%%%%%
;       %%#####%###########***+====++*##*+-----::-@%%%%%%%%%
;       %%#####%%%###########**+++=+*####**+==---=@%%%%%%%##
;       %%####*%%########************####**+=====*@%%%%%%%##
;       %#####*%%###*=-----=**++++++**=:----:.-=-#@%%%%%%%%%
;       ########%#*:=#%%%#*+:-***+**=:*#*++++-:.::%%%%%%%%%%
;       ######*-++:%@@@%%%%#*+.**+*--##=++*###+-.+:#%%%%%%%%
;       ######*.#:#%@@@@@@@%%#=:=--.**+*%@@@%@*-*.*:%%%%%%%%
;       ######*::=%#%@@@@@@%@%%.*+=-##%@@@@%@+:-%.--#%%%%@@@
;       #######*+-%%%#%%@@@@@@%.%#+:#@@@@@%+=:-+%.%%%%%%@@@@
;       ########*.#%%%######%%==#*+:+%%%##*+--+#-*%%%%%%%@@@
;       #########*:+%%%#####*--##*+#--*###*-=*+-*%%%%%%%%%%@
;       ###########+-=*#**+--+#%#*+#%*=--::::-+%%%%%%%%%%%%%
;       ############%#*+++*###%%%**#%##+-=-=#%%%%%%%%%%%%%%%
;       %%%%%########%%%%%%%###*+**#*#+--==%@%%%%%%%%%%%%%%%
;       %%%%%%########%%%%%%###*+=*#*++-:=%%%%%%%%%%%%%@%%%%
;       %%%%%%%%#%%%%%%%%%%%##*****#*==--*@%%%%%%%%%%%%%%%%%
;       %%%%%%%%%%%%%%%%%%%%%###*###+---+@@%%%%%%@@@@@@%%%%%
;       ##%%%%%%%%%%%%%%%%%%%######*=+--@@@@%%%%@@@@@@@@@@@@
;       ##%%%%%%%%%%%%%%%%%%%###**##+--*@@@@@%%%@@@@@@@@@@@@
;       #%%%%%%%%%%%%%%%%%%%%###*+#*=-=%%%%%%%%%%@@@@@@@@@@@
;       %%%%%%%%%%%%%%%%%%%#%#**+=*+-+*%%%%%%%%%%@@@@@@@@%%%
;       %%%%%%%%%%%%%%%%%%%##%%###*-*%*%%%@@@@@@@@@@@@@@%%%%
;       %%%%%%%%%%%%%%%%%%%##%%##%#+##+%%%%%%@@@@@@@@@@%%%%%
;       %%%%%%%%%%%%%%%%%%##*#%####+#*-%%@@@%%%@@@@@@@@%%%%%
;       %%%%%%%%%%%%%%%@%%##**%%%%**#*-+%%%%%%%%@@@%@@%%%%%%
;       %%%%@@@%%%%%%%@%####*+###%**#*-=*%%%%%%%%@@@%%%@%%%%
;       %%%@@@@@@%%%%%#**#%#*+***##***-+###%%%%%%%@@@@@@@%%%
;       %%%@@@@@@%%#**+**##**+*++*#+**-=#****#%%%%@@@@@@@@%%
;       %%@@@@@%##*******#***=+++**=+*+=*#*****##%%%@@%%%%%%
;       %@@@%%#**************+++**+=*+#*###**+****#%%%%%%%%%
;----------------------------------------------------------------------------------------------------------------------
; Início da SRAM: 0x0200
; Fim da SRAM: 0x03FF
; Tamanho total: 512 bytes
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
							.cdecls C,LIST,"msp430.h"
;----------------------------------------------------------------------------------------------------------------------
							.global		main
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
							.def		RESET
;----------------------------------------------------------------------------------------------------------------------
;										Instruções
;----------------------------------------------------------------------------------------------------------------------
DUMMIE_BYTE					.equ		000H
RDSR						.equ		005H
WRSR						.equ		001H
READ						.equ		003H
WRITE						.equ		002H
;----------------------------------------------------------------------------------------------------------------------
;										Modos de Operação dos Chips
;----------------------------------------------------------------------------------------------------------------------
MODO_OPERACAO_SEQ_BYTE		.equ		002H
MODO_OPERACAO_POR_PAGINA	.equ		082H
MODO_OPERACAO_SEQUENCIAL	.equ		042H
SET_MOD_OPERACAO_POR_PAGINA	.equ		080H
;----------------------------------------------------------------------------------------------------------------------
;										Operadores
;----------------------------------------------------------------------------------------------------------------------
CONTROLE_RECEP				.equ		R11
ESCRITA_IO					.equ		R12
LEITURA_IO					.equ		R13
ENDERECO_BYTE_MSB			.equ		R14
ENDERECO_BYTE_LSB			.equ		R15
;----------------------------------------------------------------------------------------------------------------------
;										Status
;----------------------------------------------------------------------------------------------------------------------
VERIFICAR_MODO_OPERACAO_CI	.equ		0ABH
OPERACAO_CONCLUIDA			.equ		0CCH
SET_MOD_OPER_POR_PAGINA		.equ		0BCH
CARREGAR_MEMORIA_SRAM		.equ		02BH
LER_MEMORIA_SRAM			.equ		0B2H
;----------------------------------------------------------------------------------------------------------------------
;										Controladores
;----------------------------------------------------------------------------------------------------------------------
CARREGAR_SESSAO				.equ		0200H
STATUS_FIRM					.equ		0202H
CONTADOR_RECEPCAO			.equ		0204H
TAMANHO_BLOCO_PAGINA		.equ		0206H
AREA_MEMORIA_SAIDA			.equ		0208H
BLOCO_PAGINA				.equ		020AH
MEMO_LEITURA_ESCRITA_SRAM	.equ		020CH
;----------------------------------------------------------------------------------------------------------------------
							.text
							.retain
							.retainrefs
;----------------------------------------------------------------------------------------------------------------------
RESET						mov.w		#__STACK_END,SP
StopWDT						mov.w		#WDTPW|WDTHOLD,&WDTCTL
							call		#CONFIGURA_CLOCK_INTERNO_A_1MZH
							call		#LIMPAR_REGISTRADORES
							call		#LIMPAR_MEMORIA_RAM
							call		#CONFIGURAR_CRISTAL_EXTERNO_32_768_KHZ
							mov.w		#00020H,&TAMANHO_BLOCO_PAGINA
							mov.w		#00220H,&AREA_MEMORIA_SAIDA
							mov.w		#00004H,&BLOCO_PAGINA
							call		#CONFIGURA_PORTAS_SAIDA
							eint
							mov.w		#00000H,R12
							call		#CONFIGURA_COMUNICACAO_SPI
							mov.b		#VERIFICAR_MODO_OPERACAO_CI,&STATUS_FIRM
							mov.w		#sessao_ver_mod_oper_sram,&CARREGAR_SESSAO
							jmp			main
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
;							LOOP PRINCIPAL
;----------------------------------------------------------------------------------------------------------------------
main						cmp.b		#VERIFICAR_MODO_OPERACAO_CI,&STATUS_FIRM
							jeq			ir_para_sessao
							cmp.b		#SET_MOD_OPER_POR_PAGINA,&STATUS_FIRM
							jeq			ir_para_sessao
							cmp.b		#CARREGAR_MEMORIA_SRAM,&STATUS_FIRM
							jeq			ir_para_sessao
							cmp.b		#LER_MEMORIA_SRAM,&STATUS_FIRM
							jeq			ir_para_sessao
							cmp.b		#OPERACAO_CONCLUIDA,&STATUS_FIRM
							jmp			$
							jmp			sessao_erro_firmware_ligar
ir_para_sessao				mov.w		&CARREGAR_SESSAO,PC
;----------------------------------------------------------------------------------------------------------------------
;							AREA DE SESSÃO
;----------------------------------------------------------------------------------------------------------------------
sessao_erro_firmware_ligar	bis.b		#BIT0,&P1OUT
sessao_erro_firmware		nop
							jmp			sessao_erro_firmware_ligar
;----------------------------------------------------------------------------------------------------------------------
sessao_ver_mod_oper_sram	call		#ATIVA_INTERRUPCAO_TEMPO_15S
							call		#SELECIONAR_CHIP
							mov.b		#RDSR,ESCRITA_IO
							call		#ENVIAR_BYTE
							mov.b		#DUMMIE_BYTE,ESCRITA_IO
							call		#ENVIAR_BYTE
							call		#LIBERAR_CHIP
							cmp.b		#MODO_OPERACAO_SEQ_BYTE,LEITURA_IO
							jeq			operacao_padrao_detectado
							cmp.b		#MODO_OPERACAO_POR_PAGINA,LEITURA_IO
							jeq			operacao_alvo_detectado
							jmp			sessao_erro_firmware_ligar
operacao_padrao_detectado	mov.b		#SET_MOD_OPER_POR_PAGINA,&STATUS_FIRM
							mov.w		#sessao_config_oper_p_p,&CARREGAR_SESSAO
							jmp			bye_sess_svmos
operacao_alvo_detectado		mov.b		#CARREGAR_MEMORIA_SRAM,&STATUS_FIRM
                            mov.w		#sessao_carrega_ram,&CARREGAR_SESSAO
                            jmp			bye_sess_svmos
operacao_ok					mov.b		#OPERACAO_CONCLUIDA,&STATUS_FIRM
bye_sess_svmos				jmp			main
;----------------------------------------------------------------------------------------------------------------------
sessao_config_oper_p_p		call		#SELECIONAR_CHIP
							mov.b		#WRSR,ESCRITA_IO
							call		#ENVIAR_BYTE
							mov.b		#SET_MOD_OPERACAO_POR_PAGINA,ESCRITA_IO
							call		#ENVIAR_BYTE
							call		#LIBERAR_CHIP
							call		#SELECIONAR_CHIP
							mov.b		#RDSR,ESCRITA_IO
							call		#ENVIAR_BYTE
							mov.b		#DUMMIE_BYTE,ESCRITA_IO
							call		#ENVIAR_BYTE
							call		#LIBERAR_CHIP
							cmp.b		#MODO_OPERACAO_POR_PAGINA,LEITURA_IO
							jeq			ok_cfg_oper_p_p
							jmp			err_cfg_oper_p_p
ok_cfg_oper_p_p				mov.b		#CARREGAR_MEMORIA_SRAM,&STATUS_FIRM
                            mov.w		#sessao_carrega_ram,&CARREGAR_SESSAO
                            jmp			main
err_cfg_oper_p_p			jmp			sessao_erro_firmware_ligar
;----------------------------------------------------------------------------------------------------------------------
sessao_carrega_ram			call		#GRAVAR_FRASE_NA_SRAM
							mov.w		#0FFFFH,R6
							mov.w		#LER_MEMORIA_SRAM,&STATUS_FIRM
							mov.w		#sessao_ler_ram,&CARREGAR_SESSAO
;----------------------------------------------------------------------------------------------------------------------
sessao_ler_ram				call		#EXIBIR_FRASE
sesso_ct_apagar				nop
							jmp			sesso_ct_apagar
;----------------------------------------------------------------------------------------------------------------------
;							FIM DA ÁREA DE SESSÃO
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
OBTER_ENDERECO_MSB_LSB:
;----------------------------------------------------------------------------------------------------------------------
							push.w		R4
							mov.w		#MEMO_LEITURA_ESCRITA_SRAM,R4
							mov.b		@R4,ENDERECO_BYTE_LSB
							add.w		#001H,R4
							mov.b		@R4,ENDERECO_BYTE_MSB
							pop.w		R4
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
SELECIONAR_CHIP:
;----------------------------------------------------------------------------------------------------------------------
							bic.b		#BIT3,&P1OUT ; Coloca CS em 0
							nop
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
LIBERAR_CHIP:
;----------------------------------------------------------------------------------------------------------------------
							bis.b		#BIT3,&P1OUT ; Coloca CS em 1
							nop
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
ENVIAR_BYTE:
;----------------------------------------------------------------------------------------------------------------------
							mov.w		#00000H,LEITURA_IO
aguarda_transmissor			bit.b   	#UCB0TXIFG,&IFG2
							jz			aguarda_transmissor
							mov.b		ESCRITA_IO,&UCB0TXBUF
aguarda_recepcao_byte		bit.b   	#UCB0RXIFG,&IFG2
							jz      	aguarda_recepcao_byte
data_ok						mov.b		&UCB0RXBUF,LEITURA_IO
							inc.b		&CONTADOR_RECEPCAO
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
CONFIGURA_CLOCK_INTERNO_A_1MZH:
;----------------------------------------------------------------------------------------------------------------------
							mov.b		&CALBC1_1MHZ,&BCSCTL1  ; Configurando para processar o relógio interno
							mov.b		&CALDCO_1MHZ,&DCOCTL   ; a 1MHz
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
LIMPAR_REGISTRADORES:
;----------------------------------------------------------------------------------------------------------------------
							mov.w		#00000H,R4
							mov.w		#00000H,R5
							mov.w		#00000H,R6
							mov.w		#00000H,R7
							mov.w		#00000H,R8
							mov.w		#00000H,R9
							mov.w		#00000H,R10
							mov.w		#00000H,R11
							mov.w		#00000H,R12
							mov.w		#00000H,R13
							mov.w		#00000H,R14
							mov.w		#00000H,R15
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
LIMPAR_MEMORIA_RAM:
;----------------------------------------------------------------------------------------------------------------------
							push.w		R4
							push.w		R5
							mov.w		#00000H,R5
							mov.w		#001F4H,R4
continue_limpar_ram			cmp.w		#00000H,R4
							jeq			fim_limpeza_ram
							mov.b		#000H,0200H(R5)
							inc.w		R5
							dec.w		R4
							jmp			continue_limpar_ram
fim_limpeza_ram				pop.w		R5
							pop.w		R4
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
CONFIGURA_PORTAS_SAIDA:
;----------------------------------------------------------------------------------------------------------------------
							bis.b		#BIT0+BIT3,&P1DIR
							bic.b		#BIT0,&P1OUT
							bis.b		#BIT3,&P1OUT
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
CONFIGURAR_CRISTAL_EXTERNO_32_768_KHZ:
;----------------------------------------------------------------------------------------------------------------------
            				bic.b   	#LFXT1S1 + LFXT1S0, &BCSCTL3
esperar_Osc_LFXT1			bic.b   	#OFIFG, &IFG1
            				nop
            				nop
            				bit.b   	#OFIFG, &IFG1
            				jnz     	esperar_Osc_LFXT1
            				ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
CONFIGURA_COMUNICACAO_SPI:
;----------------------------------------------------------------------------------------------------------------------
    						; Configura pinos P1.5 = SCLK, P1.6 = MISO, P1.7 = MOSI
    						bis.b   	#BIT5+BIT6+BIT7,&P1SEL
    						bis.b   	#BIT5+BIT6+BIT7,&P1SEL2
    						; Ativa o reset para iniciar a configuração
    						bis.b   	#UCSWRST,&UCB0CTL1
    						; Configura SPI: Master, 3-pin, 8-bit, MSB first, SPI mode 0
    						mov.b   	#UCCKPH+UCMSB+UCMST+UCSYNC,&UCB0CTL0
    						; Seleciona SMCLK (1 MHz) como clock
    						bis.b   	#UCSSEL_2,&UCB0CTL1
    						; Define divisor do clock SPI = 4 → 250 kHz
   	 						mov.b   	#04h,&UCB0BR0
    						clr.b   	&UCB0BR1
    						; Libera para o uso
    						bic.b   	#UCSWRST,&UCB0CTL1
    						ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
ATIVA_INTERRUPCAO_TEMPO_15S: ; Inicializa o timer principal que esta configurado para interromper o sistema a cada 15 segundos
;----------------------------------------------------------------------------------------------------------------------
            				bic.b   	#MC_3,&TACTL
            				clr     	&TAR
        					mov.w   	#0F000H,&TACCR0
        					mov.w   	#TASSEL_1+ID_3+MC_1,&TACTL
        					bis.w   	#CCIE,&TACCTL0
        					ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
GRAVAR_FRASE_NA_SRAM:
;----------------------------------------------------------------------------------------------------------------------
							push.w		R7
							push.w		R8
							push.w		R9
							push.w		R10
							mov.w		#00000H,R8
							mov.w		&BLOCO_PAGINA,R7
							mov.w		&TAMANHO_BLOCO_PAGINA,R9
							mov.w		#FRASE_STEPHEN_HAWKING,R10
							mov.w		#00000H,&MEMO_LEITURA_ESCRITA_SRAM
continue_escrita_pagina		cmp.w		#00000H,R7
							jeq			fim_escrita_pagina
							nop
							call		#SELECIONAR_CHIP
							mov.b		#WRITE,ESCRITA_IO
							call		#ENVIAR_BYTE
							call		#OBTER_ENDERECO_MSB_LSB
							mov.b		ENDERECO_BYTE_MSB,ESCRITA_IO
							call		#ENVIAR_BYTE
							mov.b		ENDERECO_BYTE_LSB,ESCRITA_IO
							call		#ENVIAR_BYTE
continue_area_memo_frase_ii	cmp.w		#00000H,R9
							jeq			fim_area_memo_frase_ii
							mov.b		@R10+,ESCRITA_IO
							call		#ENVIAR_BYTE
							inc.w		R8
							dec.w		R9
							jmp			continue_area_memo_frase_ii
fim_area_memo_frase_ii		call		#LIBERAR_CHIP
							mov.w		R8,&MEMO_LEITURA_ESCRITA_SRAM
							mov.w		&TAMANHO_BLOCO_PAGINA,R9
							dec.w		R7
							jmp			continue_escrita_pagina
fim_escrita_pagina			nop
							pop.w		R10
							pop.w		R9
							pop.w		R8
							pop.w		R7
							ret
;----------------------------------------------------------------------------------------------------------------------
EXIBIR_FRASE:
;----------------------------------------------------------------------------------------------------------------------
							push.w		R7
							push.w		R8
							push.w		R9
							push.w		R10
							mov.w		&BLOCO_PAGINA,R7
							mov.w		#00000H,R8
							mov.w		#00000H,R9
							mov.w		#00000H,R10
							mov.w		&AREA_MEMORIA_SAIDA,R10
							mov.w		&TAMANHO_BLOCO_PAGINA,R9
							mov.w		#00000H,&MEMO_LEITURA_ESCRITA_SRAM
continue_leitura_pagina		cmp.w		#00000H,R7
							jeq			fim_leitura_pagina
							nop
							call		#SELECIONAR_CHIP
							mov.b		#READ,ESCRITA_IO
							call		#ENVIAR_BYTE
							call		#OBTER_ENDERECO_MSB_LSB
							mov.b		ENDERECO_BYTE_MSB,ESCRITA_IO
							call		#ENVIAR_BYTE
							mov.b		ENDERECO_BYTE_LSB,ESCRITA_IO
							call		#ENVIAR_BYTE
							mov.b		#DUMMIE_BYTE,ESCRITA_IO
continue_exibir_frase		cmp.w		#00000H,R9
							jeq			fim_exibir_frase
							call		#ENVIAR_BYTE
							mov.b		LEITURA_IO,0(R10)
							inc.w		R8
							dec.w		R9
							inc.w		R10
							jmp			continue_exibir_frase
fim_exibir_frase			call		#LIBERAR_CHIP
							mov.w		R8,&MEMO_LEITURA_ESCRITA_SRAM
							mov.w		&TAMANHO_BLOCO_PAGINA,R9
							dec.w		R7
							jmp			continue_leitura_pagina
fim_leitura_pagina			nop
							pop.w		R10
							pop.w		R9
							pop.w		R8
							pop.w		R7
							ret
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
ISR_POR_TEMPO:
;----------------------------------------------------------------------------------------------------------------------
							nop
go_out						reti
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
;							SAÍDA NO CONSOLE EM MODO DEBUG
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
; 128 Bytes
; 4 páginas de 32 Bytes
;----------------------------------------------------------------------------------------------------------------------
;O maior inimigo do conhecimento não é a ignorância, é a ilusão do conhecimento.
;Por Stephen Hawking
;----------------------------------------------------------------------------------------------------------------------
FRASE_STEPHEN_HAWKING		.byte		04FH,020H,06DH,061H,069H,06FH,072H,020H,069H,06EH,069H,06DH,069H,067H,06FH
							.byte		020H,064H,06FH,020H,063H,06FH,06EH,068H,065H,063H,069H,06DH,065H,06EH,074H
							.byte		06FH,020H,06EH,0C3H,0A3H,06FH,020H,0C3H,0A9H,020H,061H,020H,069H,067H,06EH
							.byte		06FH,072H,0C3H,0A2H,06EH,063H,069H,061H,02CH,020H,0C3H,0A9H,020H,061H,020H
							.byte		069H,06CH,075H,073H,0C3H,0A3H,06FH,020H,064H,06FH,020H,063H,06FH,06EH,068H
							.byte		065H,063H,069H,06DH,065H,06EH,074H,06FH,02EH,00DH,00AH,050H,06FH,072H,020H
							.byte		053H,074H,065H,070H,068H,065H,06EH,020H,048H,061H,077H,06BH,069H,06EH,067H
							.byte		020H,020H,020H,020H,020H,020H,020H,020H,020H,020H,020H,020H,020H,020H,020H
							.byte		020H,020H,020H,020H,020H,020H,020H,020H
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
;	[-@|@-]					Stack Pointer definition
;----------------------------------------------------------------------------------------------------------------------
							.global		__STACK_END
							.sect		.stack
;----------------------------------------------------------------------------------------------------------------------
;----------------------------------------------------------------------------------------------------------------------
;	[-@|@-]					Interrupt Vectors
;----------------------------------------------------------------------------------------------------------------------
							.sect		".reset"
							.short		RESET
							.sect   	".int09"
            				.short  	ISR_POR_TEMPO
;----------------------------------------------------------------------------------------------------------------------
